# https://github.com/collectd/collectd/blob/14c59711e8470798428845ea2ecbdbe28fceb164/src/liboconfig/parser.y#L4
# https://www.rubydoc.info/gems/racc
# https://github.com/ruby/racc/tree/master/doc/en

class CollectdConfigParser
options no_result_var
rule
  target: entire_file
  
  string:
    QUOTED_STRING
    { unquote(val[0]) }
    | UNQUOTED_STRING

  argument:
    NUMBER
    | BTRUE
    | BFALSE
    | string

  argument_list:
    argument_list argument
    | argument
    { [val[0]] }

  identifier:
    UNQUOTED_STRING

  option:
    identifier argument_list EOL
    { Option.new val[0], val[1] }

  block_begin:
    OPENBRAC identifier CLOSEBRAC EOL
    { [val[1]] }
    | OPENBRAC identifier argument_list CLOSEBRAC EOL
    { [val[1], val[2]] }

  block_end:
    OPENBRAC SLASH identifier CLOSEBRAC EOL
    { val[2] }

  block:
    block_begin statement_list block_end
    { Block.new val[0][0], val[0][1], val[1] }
    | block_begin block_end
    { Block.new val[0][0], val[0][1] }

  statement:
    option
    | block
    | EOL
    { nil }
  
  statement_list:
    statement_list statement
    { val[0]&.+ [val[1]] }
    | statement
    { [val[0]] }
  
  entire_file:
    statement_list
    { val[0]&.compact || [] }
    | /* epsilon */
    { [] }

end

---- header
require "./src/config/config.rex"

---- inner
def unquote str
  str.delete_prefix('"').delete_suffix('"')
end
